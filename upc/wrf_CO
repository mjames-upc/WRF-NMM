#!/bin/csh -f
#
# run NMM WRF centered over Colorado
#
# M.James 	01/14	Copied form wrf_PRIMARY
#			Removed ldmsend - this is just for web products

source /machine/gempak/NAWIPS/Gemenviron
set TOP=/machine/gempak/wrf/rtmodel

set NX=101
set NY=155
set DX=6000
set DY=6000
echo beginning $0
if ( ! $?WRF ) then
   echo sourcing WRF cshrc
   source /machine/gempak/wrf/WRF.cshrc
endif

if ( $#argv > 0 ) then
   set YMD=$1
   set CYCLE=$2
else
   set YMD=`date -u '+%Y%m%d'`
   set CYCLE=`date -u '+%H'`
endif

set FDATE="${YMD}/${CYCLE}00"
set YMDH="${YMD}${CYCLE}"

echo FDATE is $FDATE
echo YMDH is $YMDH
echo CYCLE is $CYCLE
   
set DOMAIN_NAME=${YMDH}_12km_co
set GEMNMM=nmm_co
set RUNNAME=co

echo DOMAIN_NAME is $DOMAIN_NAME
echo GEMNMM is $GEMNMM
echo RUNNAME is $RUNNAME

# make our working directories
cd $DATA_DOMS/colorado

cp $WRF/upc/nest_info.txt .
echo $DATAROOT >! dataroot.txt


#
# create wrfsi.nl file
#
set YYYY_START=`echo $YMDH | cut -c1-4`
set MONTH_START=`echo $YMDH | cut -c5,6`
set DAY_START=`echo $YMDH | cut -c7,8`
set HOUR_START=`echo $YMDH | cut -c9,10`

set ENDTIME=`datetime $FDATE 30 '%Y%m%d%H'`
set YYYY_STOP=`echo $ENDTIME | cut -c1-4`
set MONTH_STOP=`echo $ENDTIME | cut -c5,6`
set DAY_STOP=`echo $ENDTIME | cut -c7,8`
set HOUR_STOP=`echo $ENDTIME | cut -c9,10`

set DEFCLAT="40.035"
set DEFCLON="-105.2436"
echo DEFCLAT $DEFCLAT DEFCLON $DEFCLON

cat $WRF/upc/wrfsi.nl_12km | sed 's/@YYYY_START@/'${YYYY_START}'/g' | \
   sed 's/@MONTH_START@/'${MONTH_START}'/g' | \
   sed 's/@DAY_START@/'${DAY_START}'/g' | \
   sed 's/@HOUR_START@/'${HOUR_START}'/g' | \
   sed 's/@YYYY_STOP@/'${YYYY_STOP}'/g' | \
   sed 's/@MONTH_STOP@/'${MONTH_STOP}'/g' | \
   sed 's/@DAY_STOP@/'${DAY_STOP}'/g' | \
   sed 's/@HOUR_STOP@/'${HOUR_STOP}'/g' | \
   sed 's/@NX@/'${NX}'/g' | sed 's/@NY@/'${NY}'/g' | \
   sed 's/@DX@/'${DX}'/g' | sed 's/@DY@/'${DY}'/g' | \
   sed 's/@CLAT@/'${DEFCLAT}'/g' | \
   sed 's/@CLON@/'${DEFCLON}'/g' >! wrfsi.nl 

if ( ! -e $DATAROOT/${DOMAIN_NAME} ) mkdir $DATAROOT/${DOMAIN_NAME}
cd $DATAROOT/${DOMAIN_NAME}

# added -m flag to prevent gridgem_model.exe from recreating static file
$WRF/etc/window_domain_rt.pl \
	-w wrfsi.rotlat \
	-s $DATA_SI \
	-i $WRF \
	-d $DATAROOT/${DOMAIN_NAME} \
	-t $DATA_DOMS/colorado \
	-m

# now copy over static netCDF file
cp -r $DATAROOT/colorado/static/static.wrfsi.rotlat $DATAROOT/${DOMAIN_NAME}/static/

# calculate tiles to be used
cd $TOP/g218tiles
#set TILES=`tile_calc $DEFCLAT $DEFCLON $NX $NY $DX $DY`
set TILES=`tile_calc $DEFCLAT $DEFCLON $NX $NY 8000 8000`
echo "Running wrf_prep with $TILES"
echo entering $DATAROOT/${DOMAIN_NAME}
echo CYCLE is $CYCLE
echo wrf_prep --sfcdset ssthr --dset gfsgrb2 --ftp ncep --cycle ${CYCLE}:0:30
cd $DATAROOT/${DOMAIN_NAME}

@ MAXATTEMPT=5
@ DONE=0
@ TRYNUM=0
while ( ( ! $DONE ) && ( $TRYNUM < $MAXATTEMPT ) )
   wrf_prep --sfcdset ssthr --dset gfsgrb2 --ftp ncep --cycle ${CYCLE}:0:30
   set STATUS_PREP=$status
   if ( $STATUS_PREP == 0 ) then
      set DONE=1
      # copy the ssthr data set
      set SSTHR=rtgssthr_grb_0.083
      set SAVDIR=/machine/gempak/wrf/data/grib/ssthr
      if ( ! -e $SAVDIR ) mkdir -p $SAVDIR
      if ( -e grib/${SSTHR} ) then
	 if ( -e ${SAVDIR}/${SSTHR} ) then
            cmp -s grib/${SSTHR} ${SAVDIR}/${SSTHR}
            set ISDIF=$status
	    if ( $ISDIF != 0 ) cp -p grib/${SSTHR} ${SAVDIR}/${SSTHR}
         else
            cp -p grib/${SSTHR} ${SAVDIR}/${SSTHR}
         endif
      endif
   else
      # wait, and try again shorty
      sleep 300
   endif
   @ TRYNUM = $TRYNUM + 1
end

set NOTDONE=0
set RETRY=0
while ( ( $NOTDONE == 0 ) && ( $RETRY < 2 ) )
   wrf_run --verbose
   set STATUS=$status
   if ( $STATUS != 0 ) then
      echo "wrf run ${DOMAIN_NAME} failed" | mail -s "wrf run error" mjames@unidata.ucar.edu
      wrf_clean --level 2
      @ RETRY = $RETRY + 1
   else
      set NOTDONE=1
   endif
end

if ( $NOTDONE == 0 ) then
   echo "**********************************************"
   echo "wrf run ${DOMAIN_NAME} didn't run retry $RETRY" 
   echo "**********************************************"
   echo "wrf run ${DOMAIN_NAME} didn't run" | mail -s "wrf run died" mjames@unidata.ucar.edu
   exit -1
endif

wrf_post --grib --DM
#
# check the wrfpost archive and rename it
if ( -e /machine/gempak/wrf_archive/grib/${YMDH} ) mv /machine/gempak/wrf_archive/grib/${YMDH} /machine/gempak/wrf_archive/grib/${YMDH}_${RUNNAME}

if ( -e wrfpost/grib ) then
   if ( ! -e gempak ) mkdir gempak
   cat wrfpost/grib/*_nmm_d01.GrbF* | dcgrib2 -d log/dcgrib2.log -v 1 gempak/YYYYMMDDHHfFFF_${RUNNAME}.gem
   chmod 664 gempak/*_${RUNNAME}.gem
   rm -rf ../latest
   ln -s . ../latest
   cd gempak
   scp *_${RUNNAME}.gem ldm@data:${MODEL}/wrf
   cd ..
   #$TOP/${GEMNMM}_gdplot2_output.csh ${YMDH}

   cd wrfpost/grib
   set FILES=`ls *_nmm_d01.GrbF*`
   foreach FILE ($FILES)
      set NEWFILE=`echo $FILE | sed "s@_nmm_d01@_${GEMNMM}@g"`
      ln -s $FILE $NEWFILE
   end

   cd ../..
endif

if ( ! -d $WRF/logs/runs ) mkdir $WRF/logs/runs
mv log $WRF/logs/runs/${YMDH}_${GEMNMM}


exit

